dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)

AC_INIT([blitz],[0.7],[blitz-bugs@oonumerics.org])

AC_CONFIG_AUX_DIR([config])
AC_CONFIG_SRCDIR([blitz/blitz.h])
AC_CONFIG_HEADERS([blitz/config.h])

AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE([foreign 1.8 dist-bzip2 nostdinc])

dnl Avoid generating makefile rules to rebuild maintainer-only files by
dnl default.  Maintainers may override this default and generate these
dnl makefile rules using the `--enable-maintainer-mode' configure option.
AM_MAINTAINER_MODE

dnl Blitz++ configure utility, generated by autoconf.
dnl
dnl If you want to modify this file, make sure you are editing
dnl configure.in, and not configure.  

AC_MSG_NOTICE([

Configuring $PACKAGE_STRING for $target

])

dnl C++ compiler
AC_LANG_CPLUSPLUS
AC_PROG_CXX([xlC CC cxx aCC g++ icc pgCC KCC FCC])
AC_CXX_FLAGS_PRESET

dnl Fortran compiler
AC_LANG_FORTRAN77
AC_PROG_F77([xlf f77 g77 ifort ifc pgf77 fort77])
AC_LANG([Fortran])
AC_PROG_FC([xlf90 f90 ifort ifc pgf90])
AC_FC_FREEFORM
AC_FC_SRCEXT(f90)
AC_FORTRAN_FLAGS_PRESET

dnl Doxygen documentation
AC_PROG_DOXYGEN

AC_LANG_CPLUSPLUS
dnl Disable shared library building by default (use --enable-shared to enable)
AM_DISABLE_SHARED
AC_PROG_LIBTOOL

AC_CHECK_LIB(m, sin)

AC_LIB_BLAS

dnl Check if the script was called with --enable-optimize or
dnl --enable-debug, and if so modify the compilation flags.
AC_CXX_ENABLE_OPTIMIZE
AC_CXX_ENABLE_DEBUG

dnl Check if the script was called with --enable-64bit
dnl and if so modify the compilation flags.
AC_COMPILERS_ENABLE_64BITS

dnl autoconf replacement of bzconfig
AC_CHECK_CXX_FEATURES

dnl Create the lib subdirectory by hand, since it has no Makefile.in
if test ! -d lib ; then
  mkdir lib
fi

dnl Summarize variables
AC_ENV

dnl Process the Makefile.in's
AC_CONFIG_FILES([
	Makefile 
	bin/Makefile
	blitz/Makefile
	blitz/array/Makefile
	blitz/generate/Makefile
	blitz/meta/Makefile
	random/Makefile
	compiler/Makefile
	src/Makefile 
	lib/Makefile
	manual/Makefile
	manual/examples/Makefile
	manual/stencils/Makefile
	testsuite/Makefile 
	examples/Makefile
	benchmarks/Makefile 
	benchmarks/plot_benchmarks.m
	doc/Makefile
	doc/examples/Makefile
	doc/stencils/Makefile
	doc/doxygen/Makefile
	])
dnl	demos/Makefile

AC_CONFIG_FILES([doc/doxygen/Doxyfile])

AC_OUTPUT

dnl At some point in the future it would be nice to collect
dnl information about machines, OSes and compilers which
dnl people are using.  Obviously this has to be handled in
dnl a sensitive way.  Current idea: send the output
dnl of uname -a, $(target) and $(bz_compiler) via an
dnl anonymous remailer.  Prompt the user and give them
dnl a chance to override this behaviour.
dnl AC_SEND_CONFIG

AC_INFO

